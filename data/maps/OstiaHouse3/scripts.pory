mapscripts OstiaHouse3_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SetUpOstiaHouses
}

script OstiaHouse3Man {
    lock
    faceplayer
    goto_if_set(FLAG_OSTIA_TRADE, OstiaHouse3TradeCompleted)
    setvar(VAR_0x8008, INGAME_TRADE_MANKEY)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_0x8009, GetInGameTradeSpeciesInfo)
    if(var(VAR_TEMP_0) == 0) {
        setvar(VAR_TEMP_0, 1)
        msgbox("Hey, you're a pokémon trainer right?\p"
        "I've been trying to find a Fomantis to\n"
        "catch for the last couple weeks.\p"
        "Do you have one you'd be willing to part\n"
        "with? I'd be happy to trade you my\l"
        "Mankey in exchange for it.", MSGBOX_YESNO)
    }
    else{
        msgbox("Oh, have you changed your mind?\p"
        "I'll trade my MAnkey for a Fomantis.", MSGBOX_YESNO)
    }
    if(var(VAR_RESULT) == NO) {
        msgbox(OstiaTradeDeclined)
        release
        end
    }
    special(ChoosePartyMon)
    waitstate
    if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
        msgbox(OstiaTradeDeclined)
        release
        end
    }
    copyvar(VAR_0x8005, VAR_0x8004)
    specialvar(VAR_RESULT, GetTradeSpecies)
    if(var(VAR_RESULT) != SPECIES_FOMANTIS) {
        msgbox("Umm...\p"
        "I don't think that's a Fomantis.\p"
        "If you wanna trade for my Mankey, you\n"
        "need to bring a Fomantis to trade me.")
        release
        end
    }
    msgbox("Oh, really? You'd trade me this Fomantis?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        msgbox(OstiaTradeDeclined)
        release
        end
    }
    msgbox("Wow, thank you so much! Let's start the\n"
    "trade right now.", MSGBOX_AUTOCLOSE)
    copyvar(VAR_0x8004, VAR_0x8008)
    special(CreateInGameTradePokemon)
    special(DoInGameTradeScene)
    waitstate
    setflag(FLAG_OSTIA_TRADE)
    goto(OstiaHouse3TradeCompleted)
    release
    end
}

script OstiaHouse3TradeCompleted {
    if(var(VAR_TEMP_0) == 1) {
        msgbox("Thanks for trading! I was starting\n"
        "to think I'd never have a Fomantis.\p"
        "Be sure to take good care of Mankey.")
    }
    else{
        msgbox("How are you and Mankey doing?\n"
        "Fomantis and I get along well.")
    }
    release
    end
}

script OstiaKirlia {
    lock
    faceplayer
    playmoncry(SPECIES_KIRLIA, CRY_MODE_ENCOUNTER)
    msgbox("Kyuuuu!")
    waitmoncry
    release
}

text OstiaTradeDeclined {
    "Oh, that's too bad. Fomantis is a really\n"
    "cool pokémon. I hope I find one soon.\p"
    "Well, if you change your mind, I'll\n"
    "still offer to trade my Mankey."
}
